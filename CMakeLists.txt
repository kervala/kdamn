CMAKE_MINIMUM_REQUIRED(VERSION 2.6.0)

SET(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/CMakeModules;${CMAKE_MODULE_PATH};$ENV{CMAKE_MODULE_PATH}")

INCLUDE(common OPTIONAL)

IF(NOT COMMON_MODULE_FOUND)
  MESSAGE(FATAL_ERROR "Unable to find common.cmake! Please place it in CMakeModules subdirectory, set CMAKE_MODULE_PATH environement variable or set CMAKE_MODULE_PATH parameter to CMake.")
ENDIF(NOT COMMON_MODULE_FOUND)

SET(VERSION_MAJOR 1)
SET(VERSION_MINOR 0)
SET(VERSION_PATCH "REVISION")
SET(AUTHOR "Kervala")
SET(PRODUCT "kdAmn")
SET(DESCRIPTION "Kervala deviantArt Messaging Network")
SET(TARGET_ICON "kdamn.ico")
SET(TARGET "kdamn")
SET(YEAR "2013")

PROJECT(${PRODUCT} CXX C)

# Instruct CMake to run moc automatically when needed.
SET(CMAKE_AUTOMOC ON)

INIT_DEFAULT_OPTIONS()

# Qt doesn't use RTTI or C++ exceptions
SET_OPTION_DEFAULT(WITH_EXCEPTIONS OFF)
SET_OPTION_DEFAULT(WITH_RTTI OFF)
SET_OPTION_DEFAULT(WITH_INSTALL_LIBRARIES OFF)

SETUP_DEFAULT_OPTIONS()

INIT_BUILD_FLAGS()
SETUP_BUILD_FLAGS()

SETUP_PREFIX_PATHS(${TARGET})
GEN_CONFIG_H()

SETUP_EXTERNAL()

FIND_PACKAGE(Qt5Core)
FIND_PACKAGE(Qt5Gui)
FIND_PACKAGE(Qt5Widgets)
FIND_PACKAGE(Qt5LinguistTools)

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/src ${ZLIB_INCLUDE_DIR} ${JPEG_INCLUDE_DIR})

FILE(GLOB HEADER src/*.h)
FILE(GLOB SRC src/*.cpp)
FILE(GLOB UI ui/*.ui)
FILE(GLOB RES res/*.qrc res/*.icns res/*.ico)
FILE(GLOB TS translations/*.ts)

SET_TARGET_GUI_EXECUTABLE(${TARGET} ${SRC} ${RES} ${UI} ${HEADER} ${TS})
SET_TARGET_LABEL(${TARGET} ${PRODUCT})
TARGET_LINK_LIBRARIES(${TARGET} ${QT_LIBRARIES} ${ZLIB_LIBRARY} ${JPEG_LIBRARY})

QT5_USE_MODULES(${TARGET} Core Widgets Network Svg LinguistTools)

IF(APPLE)
  SET(MACOSX_BUNDLE_GUI_IDENTIFIER "net.kervala.kdamn")

  FIND_LIBRARY(FOUNDATION_FRAMEWORK Foundation)
  FIND_LIBRARY(COCOA_FRAMEWORK Cocoa)
  FIND_LIBRARY(CARBON_FRAMEWORK Carbon)
  FIND_LIBRARY(SYSTEMCONFIGURATION_FRAMEWORK SystemConfiguration)
  FIND_LIBRARY(SECURITY_FRAMEWORK Security)
  TARGET_LINK_LIBRARIES(${TARGET} ${FOUNDATION_FRAMEWORK} ${COCOA_FRAMEWORK} ${CARBON_FRAMEWORK} ${SYSTEMCONFIGURATION_FRAMEWORK} ${SECURITY_FRAMEWORK})
ENDIF(APPLE)

ADD_DEFINITIONS(${QT_DEFINITIONS})

IF(WITH_PCH)
  ADD_NATIVE_PRECOMPILED_HEADER(${TARGET} ${CMAKE_CURRENT_SOURCE_DIR}/src/common.h ${CMAKE_CURRENT_SOURCE_DIR}/src/common.cpp)
ENDIF(WITH_PCH)

IF(UNIX)
  CONFIGURE_FILE("${CMAKE_CURRENT_SOURCE_DIR}/res/${TARGET}.desktop.in" "${CMAKE_CURRENT_BINARY_DIR}/share/applications/${TARGET}.desktop")

  INSTALL(FILES res/${TARGET}.xpm DESTINATION share/pixmaps)
  INSTALL(FILES res/kdamn16x16.png DESTINATION share/icons/hicolor/16x16/apps RENAME ${TARGET}.png)
  INSTALL(FILES res/kdamn22x22.png DESTINATION share/icons/hicolor/22x22/apps RENAME ${TARGET}.png)
  INSTALL(FILES res/kdamn24x24.png DESTINATION share/icons/hicolor/24x24/apps RENAME ${TARGET}.png)
  INSTALL(FILES res/kdamn32x32.png DESTINATION share/icons/hicolor/32x32/apps RENAME ${TARGET}.png)
  INSTALL(FILES res/kdamn48x48.png DESTINATION share/icons/hicolor/48x48/apps RENAME ${TARGET}.png)
  INSTALL(FILES res/kdamn128x128.png DESTINATION share/icons/hicolor/128x128/apps RENAME ${TARGET}.png)
  INSTALL(FILES res/kdamn.svg DESTINATION share/icons/hicolor/scalable/apps RENAME ${TARGET}.svg)
  INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/share/applications/${TARGET}.desktop" DESTINATION share/applications)
ENDIF(UNIX)

IF(TARGET_X64)
  SET(CPACK_NSIS_INSTALL_ROOT "$PROGRAMFILES64")
ELSE(TARGET_X64)
  SET(CPACK_NSIS_INSTALL_ROOT "$PROGRAMFILES")
ENDIF(TARGET_X64)

# packaging information
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY ${DESCRIPTION})
SET(CPACK_PACKAGE_VENDOR ${AUTHOR})
#SET(CPACK_PACKAGE_DESCRIPTION_FILE ${CMAKE_SOURCE_DIR}/README)
SET(CPACK_RESOURCE_FILE_LICENSE ${CMAKE_SOURCE_DIR}/COPYING)
SET(CPACK_PACKAGE_VERSION_MAJOR ${VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${VERSION_PATCH})
SET(CPACK_PACKAGE_INSTALL_DIRECTORY ${PRODUCT})
SET(CPACK_INSTALL_CMAKE_PROJECTS "${CMAKE_BINARY_DIR};${PRODUCT};ALL;/")
SET(CPACK_PACKAGE_EXECUTABLES ${TARGET} ${PRODUCT})

# NSIS Specific Packing Setup
SET(CPACK_PACKAGE_INSTALL_REGISTRY_KEY ${PRODUCT})
SET(CPACK_NSIS_EXECUTABLES_DIRECTORY ".")
#SET(CPACK_NSIS_CREATE_ICONS "CreateShortCut '\$SMPROGRAMS\\\\$STARTMENU_FOLDER\\\\${PRODUCT}.lnk' '\$INSTDIR\\\\${TARGET}.exe'")
#SET(CPACK_NSIS_MODIFY_PATH ON)
SET(CPACK_NSIS_MUI_ICON ${CMAKE_SOURCE_DIR}/res/kdamn.ico)
SET(CPACK_NSIS_MUI_UNIICON ${CMAKE_SOURCE_DIR}/res/kdamn.ico)
SET(CPACK_NSIS_INSTALLED_ICON_NAME "${TARGET}.exe")
SET(CPACK_PACKAGE_ICON "${CMAKE_SOURCE_DIR}/res\\\\header.bmp") # 150x57
SET(CPACK_NSIS_DISPLAY_NAME ${PRODUCT})
SET(CPACK_NSIS_HELP_LINK "http://dev.kervala.net/projects/kdamn")
SET(CPACK_NSIS_URL_INFO_ABOUT "http://dev.kervala.net/projects/kdamn")
SET(CPACK_NSIS_CONTACT "kervala@gmail.com")

## Source Packages
SET(CPACK_PACKAGE_FILE_NAME "${TARGET}-${VERSION}")
SET(CPACK_SOURCE_PACKAGE_FILE_NAME "${TARGET}-${VERSION}")

IF(WIN32)
  SET(CPACK_GENERATOR "NSIS")
  SET(CPACK_SOURCE_GENERATOR "ZIP")
ELSE(WIN32)
  SET(CPACK_GENERATOR "TGZ")
  SET(CPACK_SOURCE_GENERATOR "TGZ")
ENDIF(WIN32)

set(CPACK_SOURCE_IGNORE_FILES
	"~$"
	"\\\\.hg"
	"\\\\.svn"
	"\\\\.cvsignore$"
	"^${CMAKE_SOURCE_DIR}.*/CVS/"
	"^${CMAKE_SOURCE_DIR}.*/\\\\.svn/"
	"^${CMAKE_SOURCE_DIR}/debian/"
	"^${CMAKE_SOURCE_DIR}/old/")
IF(WIN32)
  IF(${CMAKE_BUILD_TYPE} STREQUAL "Release")
    IF(_QT5)
      INSTALL(FILES "${QT_PLUGINS_DIR}/imageformats/qjpeg.dll" DESTINATION ${BIN_PREFIX}/imageformats)
      INSTALL(FILES "${QT_PLUGINS_DIR}/iconengines/qsvgicon.dll" DESTINATION ${BIN_PREFIX}/iconengines)
    ENDIF(_QT5)

    IF(_QT4)
      INSTALL(FILES "${QT_PLUGINS_DIR}/imageformats/qjpeg4.dll" DESTINATION ${BIN_PREFIX}/imageformats)
      INSTALL(FILES "${QT_PLUGINS_DIR}/iconengines/qsvgicon4.dll" DESTINATION ${BIN_PREFIX}/iconengines)
    ENDIF(_QT4)
  ENDIF(${CMAKE_BUILD_TYPE} STREQUAL "Release")
ENDIF(WIN32)

INCLUDE(CPack)
